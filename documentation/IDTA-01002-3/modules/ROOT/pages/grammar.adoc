
....
<grammar> ::= <query> | <AllRules>

<query> ::= <selectStatement>? <logicalExpression> <options>?

<selectStatement> ::= "$select" "id" "$where" 

<logicalExpression> ::= <logicalNestedExpression> | <logicalOrExpression> | <logicalAndExpression> |
                        <logicalNotExpression> | <elemMatchExpression> | <BoolLiteral> | <castToBool> | <singleComparison>

<logicalNestedExpression> ::= "(" <logicalExpression> ")"

<logicalOrExpression> ::= "$or(" <logicalExpression> ("," <logicalExpression>)+ ")"
<logicalAndExpression> ::=   "$and(" <logicalExpression> ("," <logicalExpression>)+ ")"
<logicalNotExpression> ::= "$not(" <logicalExpression> ")"

<elemMatchExpression> ::= "$elemMatch(" (<logicalAndExpression> | <logicalOrExpression> | <logicalNotExpression>) ")"

<singleComparison> ::= 
    <stringComparison> |
    <numericalComparison> |
    <hexComparison> |
    <boolComparison> |
    <dateTimeComparison> |
    <timeComparison> |
    <specificAssetIdComparison>

<allComparisons> ::= ( "$eq" | "$ne" | "$gt" | "$lt" | "$ge" | "$le" )

<stringComparison> ::= 
    ( ( "$starts-with" | "$contains" ) "(" <stringOperand> ","  <stringOperand> ")" ) |
    ( <stringOperand>  <allComparisons>  <stringOperand> ) |
    ( <stringOperand>  <allComparisons>  <FieldIdentifierString> ) |
    ( <FieldIdentifierString>  <allComparisons>  <stringOperand> )
    
<numericalComparison> ::= 
    ( <numericalOperand>  <allComparisons>  <numericalOperand> ) |
    ( <numericalOperand>  <allComparisons>  <FieldIdentifierString> ) |
    ( <FieldIdentifierString>  <allComparisons>  <numericalOperand> )

<hexComparison> ::= 
    <hexOperand>  <allComparisons>  <hexOperand>

<boolComparison> ::= 
    <boolOperand>  ( "$eq" | "$ne" )  <boolOperand>

<dateTimeComparison> ::= 
    <dateTimeOperand>  <allComparisons>  <dateTimeOperand>

<dateTimeToNum> ::=
	( "dayOfWeek(" | "dayOfMonth(" | "month(" | "year(" ) <dateTimeOperand> ")"

<timeComparison> ::= 
    <timeOperand>  <allComparisons>  <timeOperand>

<specificAssetIdComparison> ::=
    ( "$aas" | "$aasdesc" ) "#specificAssetId(" ( <specificAssetIdNameClause> | <specificAssetIdValueClause>  | <specificAssetIdNameClause> "," <specificAssetIdValueClause> ) ")"

<specificAssetIdNameClause> ::= "name" ( "$starts-with" | "$contains" | "$eq" ) <StringLiteral>
<specificAssetIdValueClause> ::= "value" ( "$starts-with" | "$contains" | "$eq" ) <StringLiteral>

<operand> ::= <stringOperand> | <numericalOperand> | <hexOperand> | <boolOperand> | <dateTimeOperand> | <timeOperand>

<stringOperand> ::= 
    <FieldIdentifierString> | <StringLiteral> | <castToString> | <SingleAttribute>

<numericalOperand> ::= 
    <NumericalLiteral> | <castToNumerical> | <dateTimeToNum>

<hexOperand> ::= 
    <HexLiteral> | <castToHex>

<boolOperand> ::= 
    <BoolLiteral> | <castToBool>

<dateTimeOperand> ::= 
    <DateTimeLiteral> | <castToDateTime> | <GlobalAttribute>

<timeOperand> ::= 
    <TimeLiteral> | <castToTime>

<castToString> ::= 
    "str(" <operand> ")"

<castToNumerical> ::= 
    "num(" <operand> ")"

<castToHex> ::= 
    "hex(" <operand> ")"

<castToBool> ::= 
    "bool(" <operand> ")"

<castToDateTime> ::= 
    "dateTime(" ( <stringOperand> ) ")"

<castToTime> ::= 
    "time(" ( <stringOperand> | <dateTimeOperand> ) ")"

<options> ::= <sortExpression>? <limitExpression>?

<sortExpression> ::= "$sort" "( id" (" asc" | " dsc") ")"

<limitExpression> ::= "$limit (" <NumericalLiteral> "," <NumericalLiteral> ")"

<AllRules> ::=
( "DEFATTRIBUTES"  <StringLiteral>  <AttributeGroup>  )*
( "DEFACL"  <StringLiteral>  <ACL>  )*
( "DEFOBJECTS"  <StringLiteral>  <ObjectGroup>  )*
( "DEFFORMULA"  <StringLiteral>  <Formula>  )*
( <AccessRule>  )*

<AccessRule> ::=
( <ACL> | <UseACL> )  
"OBJECTS:" 
( <SingleObject>  )*
( ( <ObjectGroup> | <UseObjectGroup> )  )*
"FORMULA:" 
( <Formula> | <UseFormula> )

<ACL> ::=
"ATTRIBUTES:" 
( <SingleAttribute>  )*
( ( <AttributeGroup> | <UseAttributeGroup> )  )*
"RIGHTS:" 
<Right> ( <Right>)* 
"ACCESS:" 
<Access>

<UseACL> ::=
"USEACL"  <StringLiteral>

<Right> ::=
"CREATE" | "READ" | "UPDATE" | "DELETE" | "EXECUTE" | "VIEW" | "ALL" | "TREE"

<Access> ::=
"ALLOW" | "DISABLED"

<SingleAttribute> ::=
<ClaimAttribute>
| <GlobalAttribute>
| <ReferenceAttribute>

<ClaimAttribute> ::=
"CLAIM(" <StringLiteral> ")"

<GlobalAttribute> ::=
"GLOBAL(" ( "LOCALNOW" | "UTCNOW" | "CLIENTNOW" | "ANONYMOUS" ) ")"

<ReferenceAttribute> ::=
"REFERENCE(" <StringLiteral> ")"

<AttributeGroup> ::=
( <SingleAttribute>  )*
( ( <AttributeGroup> | <UseAttributeGroup> )  )*

<UseAttributeGroup> ::=
"USEATTRIBUTES"  <StringLiteral>

<SingleObject> ::=
<RouteObject>
| <IdentifiableObject>
| <ReferableObject>
| <FragmentObject>
| <DescriptorObject>

<RouteObject> ::=
"ROUTE"  <StringLiteral>

<IdentifiableObject> ::=
"IDENTIFIABLE"  <StringLiteral>

<ReferableObject> ::=
"REFERABLE"  <StringLiteral>

<FragmentObject> ::=
"FRAGMENT"  <StringLiteral>

<DescriptorObject> ::=
"DESCRIPTOR"  <StringLiteral>

<ObjectGroup> ::=
( <SingleObject>  )*
| ( <UseObjectGroup>  )*

<UseObjectGroup> ::=
"USEOBJECTS"  <StringLiteral>

<UseFormula> ::=
"USEFORMULA"  <StringLiteral>

<Formula> ::= <logicalExpression>


<DateTimeLiteral> ::= <datetime>
<TimeLiteral> ::= <time>
<datetime> ::= <date> ( "T" | " " ) <time> ( <timezone> )?
<date> ::= <year> "-" <month> "-" <day>
<year> ::= <digit> <digit> <digit> <digit>
<month> ::= <digit> <digit>
<day> ::= <digit> <digit>
<time> ::= <hour> ":" <minute> ( ":" <second> )? ( "." <fraction> )?
<timezone> ::= ("Z" | ("+" | "-") <hour> ":" <minute>)
<hour> ::= <digit> <digit>
<minute> ::= <digit> <digit>
<second> ::= <digit> <digit>
<fraction> ::= <digit>+

<digit> ::= [0-9]
<StringLiteral> ::= "\"" ( [A-Z] | [a-z] | [0-9] | "/" | "*" | "[" | "]" | "(" | ")" | " " | "@" | "#" | "\\" | "+" | "-" | "." | ":" | "$" | "^" | "*" )+ "\""
<NumericalLiteral> ::= ("+" | "-")? ( [0-9]+ (("." [0-9]* )?) | ("." [0-9]+) ) ( ("e" | "E")? [0-9]+)?
<HexLiteral> ::= "16#" ([0-9] | [A-F])+
<BoolLiteral> ::= "true" | "false"
<FieldIdentifier> ::= <FieldIdentifierString>
<FieldIdentifierString> ::= <FieldIdentifierAAS> | <FieldIdentifierSM> | <FieldIdentifierSME> | <FieldIdentifierCD> | <FieldIdentifierAasDescriptor> | <FieldIdentifierSmDescriptor>
<FieldIdentifierAAS> ::= "$aas" "#" ( "idShort" | "id" | "assetInformation.assetKind" | "assetInformation.assetType" | "assetInformation.globalAssetId" | "assetInformation." <SpecificAssetIdsClause> | "submodels." <ReferenceClause> )
<FieldIdentifierSM> ::= "$sm" "#" ( <SemanticIdClause> | "idShort" | "id" )
<FieldIdentifierSME> ::= "$sme" ("." <idShortPath> )? "#" ( <SemanticIdClause> | "idShort" | "value" | "valueType" )
<FieldIdentifierCD> ::= "$cd" "#" ( "idShort" | "id" )
<FieldIdentifierAasDescriptor> ::= "$aasdesc" "#" ( "idShort" | "id" | "assetKind" | "assetType" | "globalAssetId" | <SpecificAssetIdsClause> | "submodelDescriptors." <SmDescriptorClause> )
<FieldIdentifierSmDescriptor> ::= "$smdesc" "#" <SmDescriptorClause>
<SmDescriptorClause> ::= <SemanticIdClause> | "idShort" | "id"

<ReferenceClause> ::= "type" | "keys" ( "[" [0-9]+ "]" )? ( ".type" | ".value" )
<SemanticIdClause> ::= "semanticId" | "semanticId." <ReferenceClause>
<SpecificAssetIdsClause> ::=  "specificAssetIds" ( "[" [0-9]+ "]" )? ( ".name" | ".value" | ".externalSubjectId." <ReferenceClause> )
<idShortPath> ::= <idShort> ( "[" [0-9]+ "]" )? ( "." <idShortPath> )*
<idShort> ::= ( [a-z] | [A-Z] ) ( [a-z] | [A-Z] | [0-9] | "_" )*
....